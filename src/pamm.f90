! This file contain the main program for the PAMM clustering in 
! both PERIODIC and NON PERIODIC space.
! Starting from a set of data points in high dimension it will first perform
! a non-parametric partitioning of the probability density and return the
! Nk multivariate Gaussian/Von Mises distributions better describing the clusters.
! Can also be run in post-processing mode, where it will read tuples and 
! classify them based the model file specified in input.
!
! Copyright (C) 2014, Piero Gasparotto and Michele Ceriotti
!
! Permission is hereby granted, free of charge, to any person obtaining
! a copy of this software and associated documentation files (the
! "Software"), to deal in the Software without restriction, including
! without limitation the rights to use, copy, modify, merge, publish,
! distribute, sublicense, and/or sell copies of the Software, and to
! permit persons to whom the Software is furnished to do so, subject to
! the following conditions:
!
! The above copyright notice and this permission notice shall be included
! in all copies or substantial portions of the Software.
!
! THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
! EXPRESS OR IMPLIED, INCLUDIng BUT NOT LIMITED TO THE WARRANTIES OF
! MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRIngEMENT.
! IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
! CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
! TORT OR OTHERWISE, ARISIng FROM, OUT OF OR IN CONNECTION WITH THE
! SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
!

      PROGRAM pamm
      USE libpamm
      
      IMPLICIT NONE
      
      DOUBLE PRECISION,PARAMETER :: INF=2**30d0

      CHARACTER(LEN=1024) :: outputfile, clusterfile          ! The output file prefix
      DOUBLE PRECISION, ALLOCATABLE :: xref(:)                ! Reference point needed to find out the important cluster
      DOUBLE PRECISION, ALLOCATABLE :: period(:)              ! Periodic lenght in each dimension
      LOGICAL periodic                                        ! flag for using periodic data
      DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:) :: distmm ! similarity matrix
      DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:) :: diff     ! temp vector used to store distances

      INTEGER D                                               ! Dimensionality of problem
      INTEGER Nk                                              ! Number of gaussians in the mixture
      INTEGER nsamples                                        ! Total number points
      INTEGER ngrid                                           ! Number of samples extracted using minmax

      INTEGER jmax,ii,jj
      DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:) :: sigma2, rgrid, wj, probnmm, &
                                                     msmu, tmpmsmu, pcluster, px
      DOUBLE PRECISION :: normwj                              ! accumulator for wj
      INTEGER, ALLOCATABLE, DIMENSION(:) :: npvoronoi, iminij, pnlist, nlist
      INTEGER, ALLOCATABLE, DIMENSION(:) :: newneighl,pnewnlist ! neigh list used to build the graph of the neighbours in the grid

      INTEGER seed                                            ! seed for the random number generator

      ! variable to set the covariance matrix
      DOUBLE PRECISION tmppks,normpks
    
      ! Array of Von Mises distributions
      TYPE(vm_type), ALLOCATABLE, DIMENSION(:) :: vmclusters

      ! Array of Gaussians containing the gaussians parameters
      TYPE(gauss_type), ALLOCATABLE, DIMENSION(:) :: clusters
      ! Array containing the input data pints
      DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:) :: x , y
      ! quick shift, roots and path to reach the root (used to speedup the calculation)
      INTEGER, ALLOCATABLE, DIMENSION(:) :: idxroot, qspath
      ! PARSER
      CHARACTER(LEN=1024) :: cmdbuffer, comment   ! String used for reading text lines from files
      INTEGER ccmd                                ! Index used to control the input parameters
      INTEGER nmsopt                              ! number of mean-shift optimizations of the cluster centers
      LOGICAL verbose, fpost                      ! flag for verbosity
      LOGICAL weighted                            ! flag for using weigheted data
      INTEGER isep1, isep2, par_count             ! temporary indices for parsing command line arguments
      DOUBLE PRECISION lambda, lambda2, msw, alpha, zeta, kderr, dummd1,dummd2

      INTEGER i,j,k,ikde,counter,dummyi1,endf ! Counters and dummy variable
      
      
!!!!!!! Default value of the parameters !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
      outputfile="out"
      clusterfile="NULL"
      fpost=.false.
      alpha=1.0d0
      zeta=0.0d0
      ccmd=0              ! no parameters specified
      Nk=0                ! number of gaussians
      nmsopt=0            ! number of mean-shift refinements
      ngrid=-1            ! number of samples extracted with minmax
      seed=12345          ! seed for the random number generator
      kderr=0.1           ! target fractional error for KDE smoothing
      lambda=-1           ! quick shift cut-off
      verbose = .false.   ! no verbosity
      weighted= .false.   ! don't use the weights
      
      D=-1
      periodic=.false.
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      !!!!!!! Command line parser !!!!!!!!!!!!!
      DO i = 1, IARGC()
         CALL GETARG(i, cmdbuffer)
         IF (cmdbuffer == "-o") THEN           ! output file
            ccmd = 2
         ELSEIF (cmdbuffer == "-gf") THEN      ! file containing Vn parmeters
            ccmd = 3
         ELSEIF (cmdbuffer == "-a") THEN       ! cluster smearing
            ccmd = 1
         ELSEIF (cmdbuffer == "-d") THEN       ! dimensionality
            ccmd = 9
         ELSEIF (cmdbuffer == "-kde") THEN       ! dimensionality
            ccmd = 10
         ELSEIF (cmdbuffer == "-seed") THEN    ! seed for the random number genarator
            ccmd = 4
         ELSEIF (cmdbuffer == "-l") THEN       ! threshold to differentiate different clusters
            ccmd = 5
         ELSEIF (cmdbuffer == "-ngrid") THEN   ! N of grid points
            ccmd = 7
         ELSEIF (cmdbuffer == "-nms") THEN     ! N of mean-shift steps
            ccmd = 6
         ELSEIF (cmdbuffer == "-ref") THEN     ! point from wich calculate the distances to order
            ccmd = 8                           ! the gaussians in the output
         ELSEIF (cmdbuffer == "-z") THEN       ! add a background to the probability mixture
            ccmd = 12
         ELSEIF (cmdbuffer == "-w") THEN       ! use weights
            weighted = .true.
         ELSEIF (cmdbuffer == "-p") THEN       ! use periodicity
            ccmd = 11
         ELSEIF (cmdbuffer == "-v") THEN       ! verbosity flag
            verbose = .true.
         ELSEIF (cmdbuffer == "-h") THEN       ! help flag
            CALL helpmessage
            CALL EXIT(-1)
         ELSE
            IF (ccmd == 0) THEN
               WRITE(*,*) ""
               WRITE(*,*) " No parameters specified!"
               CALL helpmessage
               CALL EXIT(-1)
            ELSEIF (ccmd == 2) THEN            ! output file
               outputfile=trim(cmdbuffer)
            ELSEIF (ccmd == 3) THEN            ! model file
               fpost=.true.
               clusterfile=trim(cmdbuffer)
            ELSEIF (ccmd == 1) THEN            ! read the cluster smearing
               READ(cmdbuffer,*) alpha
            ELSEIF (ccmd == 10) THEN           ! read the cluster smearing
               READ(cmdbuffer,*) kderr
            ELSEIF (ccmd == 9) THEN            ! read the dimensionality
               READ(cmdbuffer,*) D
               ALLOCATE(xref(D))
               ALLOCATE(period(D))
               xref=0.0d0
               xref(1)=-1.0d0
               period=0.0d0
            ELSEIF (ccmd == 4) THEN ! read the seed
               READ(cmdbuffer,*) seed
            ELSEIF (ccmd == 6) THEN ! read the number of mean-shift steps
               READ(cmdbuffer,*) nmsopt
            ELSEIF (ccmd == 5) THEN ! cut-off for quick-shift
               READ(cmdbuffer,*) lambda
            ELSEIF (ccmd == 7) THEN ! number of grid points
               READ(cmdbuffer,*) ngrid
            ELSEIF (ccmd == 12) THEN ! read zeta 
               READ(cmdbuffer,*) zeta
            ELSEIF (ccmd == 8) THEN
               IF (D<0) STOP &
      "Dimensionality (-d) must precede the ref. point (-red)."
               par_count = 1
               isep1 = 0
               DO WHILE (index(cmdbuffer(isep1+1:), ',') > 0)
                  isep2 = index(cmdbuffer(isep1+1:), ',') + isep1
                  READ(cmdbuffer(isep1+1:isep2-1),*) xref(par_count)
                  par_count = par_count + 1
                  isep1=isep2
               ENDDO
               READ(cmdbuffer(isep1+1:),*) xref(par_count)
            ELSEIF (ccmd == 11) THEN ! read the periodicity in each dimension
               IF (D<0) STOP "Dimensionality (-d) must precede the periodic lenghts (-p). "
               par_count = 1
               isep1 = 0
               DO WHILE (index(cmdbuffer(isep1+1:), ',') > 0)
                  isep2 = index(cmdbuffer(isep1+1:), ',') + isep1
                  READ(cmdbuffer(isep1+1:isep2-1),*) period(par_count)
                  ! really brute, I know.
                  ! In the case the user will insert 6.28 or 3.14 as periodicity
                  ! the programm will automatically use a better accurancy for pi
                  IF (period(par_count) == 6.28d0) period(par_count) = twopi
                  IF (period(par_count) == 3.14d0) period(par_count) = twopi/2.0d0
                  par_count = par_count + 1
                  isep1=isep2
               ENDDO
               READ(cmdbuffer(isep1+1:),*) period(par_count)
               IF (period(par_count) == 6.28d0) period(par_count) = twopi
               IF (period(par_count) == 3.14d0) period(par_count) = twopi/2.0d0
               periodic=.true.
            ENDIF
         ENDIF
      ENDDO
      !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      CALL SRAND(seed) ! initialize the random number generator

      ! Check the input parameters
      IF (ccmd.EQ.0) THEN
         ! ccmd==0 : no parameters inserted
         WRITE(*,*) ""
         WRITE(*,*) &
  " Could not interpret command line arguments. Please check for errors!"
         CALL helpmessage
         CALL EXIT(-1)
      ENDIF

      ! The dimensionalty can't be hard coded by default
      IF (D.EQ.-1) THEN
         WRITE(*,*) ""
         WRITE(*,*) " Wrong usage. Insert the dimensionality!"
         CALL helpmessage
         CALL EXIT(-1)
      ENDIF
       
      ! POST-PROCESSING MODE
      ! This modality will run just specifying the -gf flag.
      ! The program will just compute the pamm probalities 
      ! for each given point 
      IF (fpost) THEN 
         IF (clusterfile.EQ."NULL") THEN
            ! the user did something wrong in the GM specifications
            WRITE(*,*) &
          " Error: insert the file containing the cluster parameters! "
            CALL helpmessage
            CALL EXIT(-1)
         ENDIF         
         OPEN(UNIT=12,FILE=clusterfile,STATUS='OLD',ACTION='READ')
         ! read the model informations from a file.
         IF(periodic)THEN
            ! PERIODIC version
            CALL readvmclusters(12,nk,vmclusters)
            CLOSE(12)
            ALLOCATE(pcluster(nk), px(vmclusters(1)%D))
           
            DO WHILE (.true.) ! read from the stdin
              READ(*,*,IOSTAT=endf) px
              IF(endf>0) STOP "*** Error occurred while reading file. ***"
              IF(endf<0) EXIT
              
              ! compute the pamm probability for the point px
              CALL pamm_p_vm(px, pcluster, nk, vmclusters, alpha, zeta)
              !!! decomment if you want to print out
              !!! just the number of the cluster with 
              !!! the higher probability 
              !!dummyi1=1
              !!DO i=1,nk
              !!   IF (pcluster(i)>pcluster(dummyi1)) dummyi1=i
              !!ENDDO
              !!WRITE(*,*) px,dummyi1
              WRITE(*,*) px,pcluster(:)
            ENDDO
            DEALLOCATE(vmclusters)
         ELSE
            ! NON-PERIODIC version
            CALL readclusters(12,nk,clusters)
            CLOSE(12)
            ALLOCATE(pcluster(nk), px(clusters(1)%D))
            
            DO WHILE (.true.) 
              READ(*,*,IOSTAT=endf) px
              IF(endf>0) STOP "*** Error occurred while reading file. ***"
              IF(endf<0) EXIT
              CALL pamm_p(px, pcluster, nk, clusters, alpha, zeta)
              dummyi1=1
              DO i=1,nk
                 IF (pcluster(i)>pcluster(dummyi1)) dummyi1=i
              ENDDO
              ! write out the number of the 
              ! cluster with the highest probability
              WRITE(*,*) px,dummyi1 ! ,pcluster(dummyi1)
            ENDDO
            DEALLOCATE(clusters)
         ENDIF
         
         DEALLOCATE(pcluster)
         ! done, go home
         CALL EXIT(-1)
      ENDIF
      
      ! CLUSTERING MODE
      ! get the data from standard input
      CALL readinput(D, weighted, nsamples, x, normwj, wj)
           
      ! If not specified, the number voronoi polyhedras
      ! are set to the square of the total number of points
      IF (ngrid.EQ.-1) ngrid=int(sqrt(float(nsamples)))

      ! Initialize the arrays, since now I know the number of
      ! points and the dimensionality
      ALLOCATE(iminij(nsamples))
      ALLOCATE(pnlist(ngrid+1),nlist(nsamples))
      ALLOCATE(pnewnlist(ngrid+1),newneighl(ngrid*3000)) ! ngrid*3000 is an arbitrary choice
                                                         ! ... if all the points have more than 3000 neighbours
                                                         ! we will be in a trouble!
      ALLOCATE(y(D,ngrid),npvoronoi(ngrid),probnmm(ngrid),sigma2(ngrid),rgrid(ngrid))
      ALLOCATE(idxroot(ngrid),qspath(ngrid),distmm(ngrid,ngrid))
      ALLOCATE(diff(D), msmu(D), tmpmsmu(D))

      ! Extract ngrid points on which the kernel density estimation is to be
      ! evaluated. Also partitions the nsamples points into the Voronoi polyhedra
      ! of the sampling points.
      IF(verbose) THEN
         WRITE(*,*) "NSamples: ", nsamples
         WRITE(*,*) "Selecting ", ngrid, " points using MINMAX"
      ENDIF

      CALL mkgrid(D,period,nsamples,ngrid,x,y,npvoronoi,iminij)

      ! Generate the neighbour list
      IF(verbose) write(*,*) "Generating neighbour list"
      CALL getnlist(nsamples,ngrid,npvoronoi,iminij, pnlist,nlist)

      ! Definition of the distance matrix between grid points
      distmm=0.0d0
      rgrid=1d100 ! "voronoi radius" of grid points (squared)
      IF(verbose) write(*,*) "Computing similarity matrix"
      DO i=1,ngrid

         DO j=1,i-1
            ! distance between two voronoi centers
            ! also computes the nearest neighbor distance (squared) for each grid point
            distmm(i,j) = pammr2(D,period,y(:,i),y(:,j))
            if (distmm(i,j) < rgrid(i)) rgrid(i) = distmm(i,j)
            ! the symmetrical one
            distmm(j,i) = distmm(i,j)
            if (distmm(i,j) < rgrid(j)) rgrid(j) = distmm(i,j)  
         ENDDO
      ENDDO
      
      IF(lambda.EQ.-1)THEN
         ! set automatically the mean shift lambda set to 5*<sig>
         lambda2=SUM(rgrid)/ngrid
         lambda=5.0d0*dsqrt(lambda2)
      ENDIF
      lambda2=lambda*lambda ! we always work with squared distances....

      sigma2 = rgrid ! initially set KDE smearing to the nearest grid distance
      ikde = 0
100   IF(verbose) write(*,*) &
            "Computing kernel density on reference points."

      ! 1. computes the KDE on the Voronoi centers using the neighbour list
      probnmm=0.0d0
      
      ! 2. here it also builds the neighbourd GRAPH : a node is connected to all its neighbours
      newneighl=0 ! initialize to zero the neighbour list
      pnewnlist(1)=0
      DO i=1,ngrid
         ! a neighbour list works as follows:
         ! pn(i) = pointer to the vector NL. 
         ! The indices of neighbours of  i  range from  pn(i)+1  to  pn(i+1)
         
         ! initialization
         ! the previous index contains the NL index of last neighbour of the previous particle
         ! we use that as starting point
         pnewnlist(i+1)=pnewnlist(i)+1
         newneighl(pnewnlist(i+1))=i ! just an initializition, then we will change it
         DO j=1,ngrid
            !! the radius in which to look for neighbour is hard coded for now
            IF (distmm(i,j)<2.0d0*rgrid(i)) THEN
               IF (i.NE.j) THEN
                  IF (newneighl(pnewnlist(i+1)).NE.i) &
                     ! The first time that a neighbour has been found
                     ! we don't move the pointer, we just modify the neighbour list
                     pnewnlist(i+1)=pnewnlist(i+1)+1
                  newneighl(pnewnlist(i+1))=j
               ENDIF
            ENDIF
            
            
            ! do not compute KDEs for points that belong to far away Voronoj
            IF (distmm(i,j)/sigma2(j)>36.0d0) CYCLE

            ! cycle just inside the polyhedra using the neighbour list
            DO k=pnlist(j)+1,pnlist(j+1)
               probnmm(i)=probnmm(i)+ wj(nlist(k))* &
                          fkernel(D,period,sigma2(j),y(:,i),x(:,nlist(k)))
            ENDDO
            
            
         ENDDO
         probnmm(i)=probnmm(i)/normwj
      ENDDO
      
      ! compute sigma2 from the number of points in the 
      ! neighborhood of each grid point. the rationale is that 
      ! integrating over a Gaussian kernel with variance sigma2
      ! will cover a volume Vi=(2\pi sigma2)^(D/2).  
      ! If the estimate probability density on grid point i is pi
      ! then the probability to be within that (smooth) bin is ri=pi * Vi. 
      ! The number of points is then normwj*ri, and we can take this to 
      ! correspond to the mean of a binomial distribution. The (squared) relative error
      ! is then err2=(1-ri)/(ri normwj) so
      ! kderr^2 *normwj + 1 = 1/ri so
      ! Vi = 1/[pi*(1+kderr^2 normwj)] so 
      ! sigma2 = 1/(2pi) 1/rad[D/2](pi*(1+kderr^2 normwj))
      DO j=1,ngrid
         !IF(verbose) WRITE(*,*) "Update grid point ", j, sigma2(j)
         sigma2(j) = 1/twopi *1/( probnmm(j)*(1+normwj*kderr*kderr))**(D/2)
         ! kernel density estimation cannot become smaller than the distance with the nearest grid point
         IF (sigma2(j).lt.rgrid(j)) sigma2(j)=rgrid(j)
         !IF(verbose) WRITE(*,*) "Prob ", probnmm(j),  " new sigma ", sigma2(j)         
      ENDDO
      ikde = ikde+1
      if (ikde<5) GOTO 100 ! seems one could actually iterate to self-consistency....

      ! Were the KDE is done and we also the graph of the neighbours
      ! We can start the clustering (quick shift), to single out the local maxima 
      IF(verbose) write(*,*) "Running quick shift"

      idxroot=0
      DO i=1,ngrid
         IF(idxroot(i).NE.0) CYCLE
         qspath=0
         qspath(1)=i
         counter=1
         DO WHILE(qspath(counter).NE.idxroot(qspath(counter)))
            idxroot(qspath(counter))= &
               qs_next(ngrid,qspath(counter),probnmm,distmm,pnewnlist,newneighl,kderr)  
            IF(idxroot(idxroot(qspath(counter))).NE.0) EXIT
            counter=counter+1
            qspath(counter)=idxroot(qspath(counter-1))
         ENDDO
         DO j=1,counter
            idxroot(qspath(j))=idxroot(idxroot(qspath(counter)))
         ENDDO
      ENDDO

      IF(verbose) write(*,*) "Writing out"
      qspath=0
      qspath(1)=idxroot(1)
      Nk=1
      normpks=0.0d0
      OPEN(UNIT=11,FILE=trim(outputfile)//".grid",STATUS='REPLACE',ACTION='WRITE')
      DO i=1,ngrid
         ! write out the clusters
         dummyi1=0
         DO k=1,Nk
            IF(idxroot(i).EQ.qspath(k))THEN
               dummyi1=k
               EXIT
            ENDIF
         ENDDO
         IF(dummyi1.EQ.0)THEN
            Nk=Nk+1
            qspath(Nk)=idxroot(i)
            dummyi1=Nk
         ENDIF
         DO j=1,D
           WRITE(11,"((A1,ES15.4E4))",ADVANCE = "NO") " ", y(j,i)
         ENDDO
         WRITE(11,"(A1,I4,A1,ES15.4E4,A1,ES15.4E4)") " ", dummyi1 , " ", probnmm(i), " ", sigma2(i) 
         ! accumulate the normalization factor for the pks
         normpks=normpks+probnmm(i)
      ENDDO
      CLOSE(UNIT=11)
      
      ! now we can procede and complete the definition of probability model
      ! now qspath contains the indexes of Nk gaussians
      IF(periodic)THEN
         ALLOCATE(vmclusters(Nk))
      ELSE
         ALLOCATE(clusters(Nk))
      ENDIF
      
      DO k=1,Nk
         IF(periodic)THEN
            ALLOCATE(vmclusters(k)%mean(D))
            ALLOCATE(vmclusters(k)%cov(D,D))
            ALLOCATE(vmclusters(k)%icov(D,D))
            ALLOCATE(vmclusters(k)%period(D))
            vmclusters(k)%period=period
            vmclusters(k)%mean=y(:,qspath(k))
         ELSE
            ALLOCATE(clusters(k)%mean(D))
            ALLOCATE(clusters(k)%cov(D,D))
            ALLOCATE(clusters(k)%icov(D,D))
            clusters(k)%mean=y(:,qspath(k))
         ENDIF
         ! optionally do a few mean-shift steps to find a better estimate 
         ! of the cluster mode
         DO j=1,nmsopt
            msmu=0.0d0
            tmppks=0.0d0
            
            DO i=1,ngrid
               ! should correct the Gaussian evaluation with a Von Mises distrib in the case of periodic data
               IF(periodic)THEN
                  msw = probnmm(i)*exp(-0.5*pammr2(D,period,y(:,i),vmclusters(k)%mean)/(lambda2/25.0d0))
               ELSE
                  msw = probnmm(i)*exp(-0.5*pammr2(D,period,y(:,i),clusters(k)%mean)/(lambda2/25.0d0))
               ENDIF
               CALL pammrij(D,period,y(:,i),vmclusters(k)%mean,tmpmsmu)
               msmu = msmu + msw*tmpmsmu
               tmppks = tmppks + msw
            ENDDO
            
            IF(periodic)THEN
               vmclusters(k)%mean = vmclusters(k)%mean + msmu / tmppks
            ELSE
               clusters(k)%mean = msmu / tmppks
            ENDIF
         ENDDO
         
         ! compute the gaussians covariance from the data in the clusters
         IF(periodic)THEN
            vmclusters(k)%cov = 0.0d0
         ELSE
            clusters(k)%cov = 0.0d0
         ENDIF
         
         tmppks=0.0d0
         DO i=1,ngrid
            IF(idxroot(i).NE.qspath(k)) CYCLE
            tmppks=tmppks+probnmm(i)
            DO ii=1,D
               DO jj=1,D
                  IF(periodic)THEN
                     ! Minimum Image Convention
                     dummd1 = (y(ii,i) - vmclusters(k)%mean(ii)) / period(ii)
                     dummd1 = dummd1 - dnint(dummd1)                          
                     dummd1 = dummd1 * period(ii)
                     
                     dummd2 = (y(jj,i)-vmclusters(k)%mean(jj)) / period(jj)
                     dummd2 = dummd2 - dnint(dummd2)                          
                     dummd2 = dummd2 * period(jj)
                     
                     vmclusters(k)%cov(ii,jj)= vmclusters(k)%cov(ii,jj)+probnmm(i)* &
                                               dummd1 * dummd2
                  ELSE
                     clusters(k)%cov(ii,jj)=clusters(k)%cov(ii,jj)+probnmm(i)* &
                       (y(ii,i)-clusters(k)%mean(ii))*(y(jj,i)-clusters(k)%mean(jj))
                  ENDIF
               ENDDO
            ENDDO
         ENDDO
         
         IF(periodic)THEN
            vmclusters(k)%cov=vmclusters(k)%cov/tmppks
            vmclusters(k)%weight=tmppks/normpks
            vmclusters(k)%D=D
         ELSE
            clusters(k)%cov=clusters(k)%cov/tmppks
            clusters(k)%weight=tmppks/normpks
            clusters(k)%D=D
         ENDIF
      ENDDO

      IF(periodic)THEN
         ! write the VM distributions
         ! write a 2-lines header containig a bit of information
         WRITE(comment,*) "# PAMM(PERIODIC) clusters analysis. NSamples: ", nsamples, " NGrid: ", &
                   ngrid, " QSLambda: ", lambda, ACHAR(10), "# Dimensionality/NClusters//Pk/Mean/Covariance "

         OPEN(UNIT=12,FILE=trim(outputfile)//".pamm",STATUS='REPLACE',ACTION='WRITE')
         ! also here we should sort in some way the clusters
         CALL writevmclusters(12, comment, nk, vmclusters)
         CLOSE(UNIT=12)
         DEALLOCATE(vmclusters)
      ELSE
         ! write gaussians
         ! oreder gaussians from the closest to the reference point
         CALL sortclusters(Nk, clusters, xref)
         
         ! write a 2-lines header
         WRITE(comment,*) "# PAMM clusters analysis. NSamples: ", nsamples, " NGrid: ", &
                   ngrid, " QSLambda: ", lambda, ACHAR(10), "# Dimensionality/NClusters//Pk/Mean/Covariance "
         
         OPEN(UNIT=12,FILE=trim(outputfile)//".pamm",STATUS='REPLACE',ACTION='WRITE')
         
         CALL writeclusters(12, comment, nk, clusters)
         CLOSE(UNIT=12)
         ! maybe I should deallocate better..
         DEALLOCATE(clusters)
      ENDIF
      
      DEALLOCATE(x,wj)
      DEALLOCATE(xref,period)
      DEALLOCATE(idxroot,qspath,distmm)
      DEALLOCATE(pnlist,nlist,iminij)
      DEALLOCATE(y,npvoronoi,probnmm,sigma2,rgrid)
      DEALLOCATE(diff,msmu,tmpmsmu)
      DEALLOCATE(pnewnlist,newneighl)

      CALL EXIT(0)
      ! end of the main programs



!!!!! FUCTIONS and SUBROUTINES !!!!!!!!!!!!!!!!!!!!

      CONTAINS

      SUBROUTINE helpmessage
         ! Banner to print out for helping purpose
         !

         WRITE(*,*) ""
         WRITE(*,*) " USAGE: pamm [-h] -d D [-p 6.28,6.28,...] [-w] [-o output] [-ngrid ngrid] "
         WRITE(*,*) "             [-l lambda] [-kde err] [-z zeta_factor] [-a smoothing_factor] "
         WRITE(*,*) "             [-seed seedrandom] [-rif -1,0,0,...] [-v] "
         WRITE(*,*) ""
         WRITE(*,*) " Applies the PAMM clustering to a high-dimensional data set. "
         WRITE(*,*) " It is mandatory to specify the dimensionality of the data, which "
         WRITE(*,*) " must be passed through the standard input in the format: "
         WRITE(*,*) " x11 x12 x13 ... x1D [w1] "
         WRITE(*,*) " x21 x22 x23 ... x2D [w2] "
         WRITE(*,*) ""
         WRITE(*,*) " For other options a default is defined.  "
         WRITE(*,*) ""
         WRITE(*,*) "   -h                : Print this message "
         WRITE(*,*) "   -d D              : Dimensionality "
         WRITE(*,*) "   -w                : Reads weights for the sample points [default: no weight] "
         WRITE(*,*) "   -o output         : Prefix for output files [out]. This will produce : "
         WRITE(*,*) "                            output.grid (clusterized grid points) "
         WRITE(*,*) "                            output.pamm (cluster parameters) "
         WRITE(*,*) "   -l lambda         : Quick shift cutoff [automatic] "
         WRITE(*,*) "   -kde err          : Target fractional error for KDE smoothing [0.1]"
         WRITE(*,*) "   -ngrid ngrid      : Number of grid points to evaluate KDE [sqrt(nsamples)]"
         WRITE(*,*) "   -nms nms          : Do nms mean-shift steps with a Gaussian width lambda/5 to"
         WRITE(*,*) "                       optimize cluster centers [0] "
         WRITE(*,*) "   -seed seed        : Seed to initialize the random number generator. [12345]"
         WRITE(*,*) "   -ref X            : Reference point for ordering the clusters [ (-1,0,0,...) ]"
         WRITE(*,*) "   -p P1,...,PD      : Periodicity in each dimension [ (6.28,6.28,6.28,...) ]"
         WRITE(*,*) "   -v                : Verbose output "
         WRITE(*,*) ""
         WRITE(*,*) " Post-processing mode (-gf): this reads high-dim data and computes the "
         WRITE(*,*) " cluster probabilities associated with them, given the output of a "
         WRITE(*,*) " previous PAMM analysis. "
         WRITE(*,*) ""
         WRITE(*,*) "   -gf               : File to read reference Gaussian clusters from"
         WRITE(*,*) "   -a                : Additional smearing of clusters "
         WRITE(*,*) "   -z zeta_factor : Probabilities below this threshold are counted as 'no cluster' [default:0]"
         WRITE(*,*) ""
      END SUBROUTINE helpmessage

      SUBROUTINE readinput(D, fweight, nsamples, xj, totw, wj)
         IMPLICIT NONE
         INTEGER, INTENT(IN) :: D
         LOGICAL, INTENT(IN) :: fweight
         INTEGER, INTENT(OUT) :: nsamples
         DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:), INTENT(OUT) :: xj
         DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:), INTENT(OUT) :: wj
         DOUBLE PRECISION, INTENT(OUT) :: totw

         ! uses a buffer to read the input reallocating the arrays when needed
         INTEGER, PARAMETER :: nbuff = 100000
         DOUBLE PRECISION :: vbuff(D,nbuff), wbuff(nbuff)
         DOUBLE PRECISION, ALLOCATABLE :: vtmp(:,:), wtmp(:)

         INTEGER io_status, counter

         nsamples = 0
         totw = 0.0d0
         counter = 0

         ! initial dummy allocation
         ALLOCATE(xj(D,1),wj(1),vtmp(D,1),wtmp(1))
         xj=0.0d0
         totw=0.0d0
         DO
            IF(fweight) THEN
               READ(5,*, IOSTAT=io_status) vbuff(:,counter+1), wbuff(counter+1)
            ELSE
               READ(5,*, IOSTAT=io_status) vbuff(:,counter+1)
               wbuff(counter+1)=1.0d0
            ENDIF
            totw=totw+wbuff(counter+1)
            IF(io_status<0 .or. io_status==5008) EXIT    ! also intercepts a weird error given by some compilers when reading past of EOF
            IF(io_status>0) STOP "*** Error occurred while reading file. ***"

            counter=counter+1

            ! grow the arrays and dump the buffers
            IF(counter.EQ.nbuff) THEN
               DEALLOCATE(wtmp,vtmp)
               ALLOCATE(wtmp(nsamples+counter), vtmp(D,nsamples+counter))
               wtmp(1:nsamples) = wj
               vtmp(:,1:nsamples) = xj
               wtmp(nsamples+1:nsamples+counter) = wbuff
               vtmp(:,nsamples+1:nsamples+counter) = vbuff

               DEALLOCATE(wj, xj)
               ALLOCATE(wj(nsamples+counter), xj(D,nsamples+counter))
               wj=wtmp
               xj=vtmp

               nsamples=nsamples+counter
               counter=0
            ENDIF
         END DO

         IF(counter>0) THEN
            DEALLOCATE(wtmp,vtmp)
            ALLOCATE(wtmp(nsamples+counter), vtmp(D,nsamples+counter))
            wtmp(1:nsamples) = wj
            vtmp(:,1:nsamples) = xj
            wtmp(nsamples+1:nsamples+counter) = wbuff(1:counter)
            vtmp(:,nsamples+1:nsamples+counter) = vbuff(:,1:counter)

            DEALLOCATE(wj, xj)
            ALLOCATE(wj(nsamples+counter), xj(D,nsamples+counter))
            wj=wtmp
            xj=vtmp

            nsamples=nsamples+counter
            counter=0
         ENDIF

      END SUBROUTINE readinput

      SUBROUTINE mkgrid(D,period,nsamples,ngrid,x,y,npvoronoi,iminij)
         ! Select ngrid grid points from nsamples using minmax and
         ! the voronoi polyhedra around them.
         !
         ! Args:
         !    nsamples: total points number
         !    ngrid: number of grid points
         !    x: array containing the data samples
         !    y: array that will contain the grid points
         !    npvoronoi: array cotaing the number of samples inside the Voronoj polyhedron of each grid point
         !    iminij: array containg the neighbor list for data samples

         INTEGER, INTENT(IN) :: D
         DOUBLE PRECISION, INTENT(IN) :: period(D)
         INTEGER, INTENT(IN) :: nsamples
         INTEGER, INTENT(IN) :: ngrid
         DOUBLE PRECISION, DIMENSION(D,nsamples), INTENT(IN) :: x
         DOUBLE PRECISION, DIMENSION(D,ngrid), INTENT(OUT) :: y
         INTEGER, DIMENSION(ngrid), INTENT(OUT) :: npvoronoi
         INTEGER, DIMENSION(nsamples), INTENT(OUT) :: iminij

         INTEGER i,j
         DOUBLE PRECISION :: dminij(nsamples), dij, dmax

         iminij=0
         y=0.0d0
         npvoronoi=0
         ! choose randomly the first point
         y(:,1)=x(:,int(RAND()*nsamples))
         dminij = 1.0d99
         iminij = 1
         DO i=2,ngrid
            dmax = 0.0d0
            DO j=1,nsamples
               dij = pammr2(D,period,y(:,i-1),x(:,j))
               IF (dminij(j)>dij) THEN
                  dminij(j) = dij
                  iminij(j) = i-1 ! also keeps track of the Voronoi attribution
               ENDIF
               IF (dminij(j) > dmax) THEN
                  dmax = dminij(j)
                  jmax = j
               ENDIF
            ENDDO
            y(:,i) = x(:, jmax)

            IF(verbose .AND. (modulo(i,1000).EQ.0)) &
               write(*,*) i,"/",ngrid
         ENDDO

         ! finishes Voronoi attribution
         DO j=1,nsamples
            dij = pammr2(D,period,y(:,ngrid),x(:,j))
            IF (dminij(j)>dij) THEN
               dminij(j) = dij
               iminij(j) = ngrid
            ENDIF
         ENDDO

         ! Number of points in each voronoi polyhedra
         npvoronoi=0
         DO j=1,nsamples
            npvoronoi(iminij(j))=npvoronoi(iminij(j))+1
         ENDDO
      END SUBROUTINE mkgrid

      SUBROUTINE dijkstra(ngrid,pnlist,nlist,distmm,source,ending,geopath)
         ! Get the geodesic path between source and ending
         !
         
         INTEGER, INTENT(IN) :: ngrid,source,ending
         INTEGER, DIMENSION(ngrid+1), INTENT(IN) :: pnlist
         INTEGER, DIMENSION(ngrid*3000), INTENT(IN) :: nlist
         DOUBLE PRECISION, DIMENSION(ngrid,ngrid), INTENT(IN) :: distmm
         INTEGER, DIMENSION(ngrid+1), INTENT(OUT) :: geopath

         INTEGER i,u,chksize
        
         INTEGER, DIMENSION(ngrid) :: prev,checklist
         DOUBLE PRECISION, DIMENSION(ngrid) :: dist
         DOUBLE PRECISION altdist
         ! initialization
         DO i=1,ngrid
            dist(i)=INF ! Distance from source to source
            prev(i)=-1      ! Previous node in optimal path initialization
         ENDDO
         
         dist(source)=0.0d0
         
         checklist=0 ! set of visited nodes.
         chksize=0 ! number of visited nodes.
         u=0
         DO WHILE(u.NE.ending) 
            u=getmindist(dist,checklist) ! the first one will be source
            ! add u to checklist of visited nodes
            ! if u==ending, we are done here
            IF(u.EQ.ending) EXIT
            chksize=chksize+1
            checklist(chksize)=u
            ! cycle inside the neighbourhood of u
            DO i=pnlist(u)+1,pnlist(u+1)
               ! here we relax the distances
               altdist=dist(u)+distmm(u,nlist(i))
               IF(altdist<dist(nlist(i)))THEN
                  dist(nlist(i))=altdist
                  prev(nlist(i))=u
               ENDIF
            ENDDO
         ENDDO
         
         ! return the geodesic path from source to ending
         ! source and ending won't be present in geopath
         geopath=0
         i=1
         u=ending
         DO WHILE(u.NE.source)
            i=i+1
            geopath(i)=prev(u)
            u=prev(u)
         ENDDO
         geopath(1)=i ! the first position of geopath is a pointer to the end of the vector
      END SUBROUTINE dijkstra 
      
      INTEGER FUNCTION getmindist(dist,checklist)
         ! Get the index corresponding to the minumum value
         DOUBLE PRECISION, DIMENSION(ngrid), INTENT(IN) :: dist
         INTEGER, DIMENSION(ngrid), INTENT(IN) :: checklist
         INTEGER i
         DOUBLE PRECISION tmpdist
         
         tmpdist=INF
         getmindist=0
         ! initialization
         DO i=1,ngrid
            IF(dist(i)<tmpdist .AND. (.NOT. isinthere(ngrid,checklist,i)))THEN
               ! we also check we did not visited yet that node
               tmpdist=dist(i)
               getmindist=i
            ENDIF
         ENDDO
      END FUNCTION getmindist
      
      LOGICAL FUNCTION isinthere(ngrid,indeces,idx)
         ! Tell if idx is present in the vector indeces
         
         INTEGER, INTENT(IN) :: ngrid,idx
         INTEGER, DIMENSION(ngrid), INTENT(IN) :: indeces

         INTEGER i
          
         isinthere=.FALSE.
         ! initialization
         DO i=1,ngrid
            IF(indeces(i)==idx) isinthere=.TRUE.! found
         ENDDO
      END FUNCTION isinthere

      SUBROUTINE getnlist(nsamples,ngrid,npvoronoi,iminij, pnlist,nlist)
         ! Build a neighbours list: for every voronoi center keep track of his
         ! neighboroud that correspond to all the points inside the voronoi
         ! polyhedra.
         !
         ! Args:
         !    nsamples: total points number
         !    ngrid: number of voronoi polyhedra
         !    npvoronoi: array cotaing the number of points inside each voroni polyhedra
         !    iminij: array containg to wich polyhedra every point belong to
         !    pnlist: pointer to neighbours list
         !    nlist: neighbours list

         INTEGER, INTENT(IN) :: nsamples
         INTEGER, INTENT(IN) :: ngrid
         INTEGER, DIMENSION(ngrid), INTENT(IN) :: npvoronoi
         INTEGER, DIMENSION(nsamples), INTENT(IN) :: iminij
         INTEGER, DIMENSION(ngrid+1), INTENT(OUT) :: pnlist
         INTEGER, DIMENSION(nsamples), INTENT(OUT) :: nlist

         INTEGER i,j
         INTEGER :: tmpnidx(ngrid)

         pnlist=0
         nlist=0
         tmpnidx=0

         ! pointer to the neighbourlist
         pnlist(1)=0
         DO i=1,ngrid
            pnlist(i+1)=pnlist(i)+npvoronoi(i)
            tmpnidx(i)=pnlist(i)+1  ! temporary array to use while filling up the neighbour list
         ENDDO

         DO j=1,nsamples
            i=iminij(j) ! this is the Voronoi center the sample j belongs to
            nlist(tmpnidx(i))=j ! adds j to the neighbour list
            tmpnidx(i)=tmpnidx(i)+1 ! advances the pointer
         ENDDO
      END SUBROUTINE getnlist

!      INTEGER FUNCTION qs_next(D,period,ngrid,idx,lambda,probnmm,distmm,y,sig2,kderr)
!         ! Return the index of the closest point higher in P there are no minima in
!         ! between
!         !
!         ! Args:
!         !    D      :    dimensionality
!         !    period :    periodicity
!         !    ngrid  :    number of grid points
!         !    idx    :    current point
!         !    lambda :    estimate of the NN ditance
!         !    probnmm:    prob density estimations
!         !    distmm :    distances matrix
!         !    y      :    grid points
!         !    sig2   :    variance to be used in the KDE when doing the minima search
!         !    kderr  :    relative error to get rid of the noise in accepting/rejecting
!         !                a minimum
!
!         INTEGER, INTENT(IN) :: D,ngrid
!         DOUBLE PRECISION, DIMENSION(D) :: period
!         INTEGER, INTENT(IN) :: idx
!         DOUBLE PRECISION, INTENT(IN) :: lambda,sig2,kderr
!         DOUBLE PRECISION, DIMENSION(ngrid), INTENT(IN) :: probnmm
!         DOUBLE PRECISION, DIMENSION(ngrid,ngrid), INTENT(IN) :: distmm
!         DOUBLE PRECISION, DIMENSION(D,ngrid), INTENT(IN) :: y ! grid points
!
!         INTEGER i,j,np
!         DOUBLE PRECISION dmin,fkde,fkder
!         DOUBLE PRECISION, ALLOCATABLE, DIMENSION(:,:) :: nppoints
!         
!         LOGICAL testlower
!
!         dmin=1.0d10
!         qs_next=idx
!         
!         DO j=1,ngrid
!            IF(probnmm(j)>probnmm(idx))THEN
!               IF(distmm(idx,j).LT.dmin) THEN
!                  !! This is a really rough trial...
!                  ! NEW with floor and proportions
!                  IF(distmm(idx,j)<lambda*2.5d0)THEN
!                     np=10
!                  ELSE
!                     np=20
!                  ENDIF
!                  ALLOCATE(nppoints(D,np))
!                  ! extract NP points between the extremes
!                  CALL getNpoint(D,period,np,y(:,idx),y(:,j),nppoints)
!                                 
!                  testlower=.FALSE.
!                  fkder=genkernel(ngrid,D,period,sig2,probnmm,y(:,idx),y)       
!                  DO i=1,np
!                     fkde=genkernel(ngrid,D,period,sig2,probnmm,nppoints(:,i),y)
!                     IF(fkde<(fkder*(1-kderr*2.0d0))) THEN
!                        testlower=.TRUE. ! maybe it is a valley
!                        EXIT
!                     ENDIF
!                  ENDDO
!                  
!                  IF(.NOT. testlower) THEN 
!                     qs_next=j
!                     dmin=distmm(idx,j)
!                  ENDIF
!                  
!                  DEALLOCATE(nppoints)
!               ENDIF
!            ENDIF
!         ENDDO
!      END FUNCTION qs_next

      INTEGER FUNCTION qs_next(ngrid,idx,probnmm,distmm,pnlist,nlist,kderr)
         ! Return the index of the closest point higher in P
         !
         ! Args:
         !    ngrid: number of grid points
         !    idx: current point
         !    probnmm: density estimations
         !    distmm: distances matrix

         INTEGER, INTENT(IN) :: ngrid
         INTEGER, INTENT(IN) :: idx
         DOUBLE PRECISION, DIMENSION(ngrid), INTENT(IN) :: probnmm
         DOUBLE PRECISION, DIMENSION(ngrid,ngrid), INTENT(IN) :: distmm
         INTEGER, DIMENSION(ngrid+1), INTENT(IN) :: pnlist
         INTEGER, DIMENSION(ngrid*3000), INTENT(IN) :: nlist
         DOUBLE PRECISION, INTENT(IN) :: kderr

         INTEGER i,j
         DOUBLE PRECISION dmin
         INTEGER, DIMENSION(ngrid+1) :: geopath
         LOGICAL testlower

         dmin=1.0d10
         qs_next=idx
         DO j=1,ngrid
            IF(probnmm(j)>probnmm(idx))THEN
               IF(distmm(idx,j).LT.dmin)THEN
                  ! we jump if there is not a valley in the geodesic path between the two point
                  ! get the geodesic path
                  CALL dijkstra(ngrid,pnlist,nlist,distmm,idx,j,geopath)
                  ! check the presence of a valley
                  testlower=.FALSE.
                  DO i=2,geopath(1)
                     IF(probnmm(geopath(i))<(probnmm(idx)*(1-kderr*2.0d0))) THEN
                        testlower=.TRUE. ! there is a valley
                        EXIT
                     ENDIF                  
                  ENDDO
                  IF(.NOT. testlower)THEN 
                     ! no valley : JUMP! 
                     dmin=distmm(idx,j)
                     qs_next=j
                  ENDIF
               ENDIF
            ENDIF
         ENDDO
      END FUNCTION qs_next

!      INTEGER FUNCTION qs_next(ngrid,idx,lambda,probnmm,distmm)
!         ! Return the index of the closest point higher in P
!         !
!         ! Args:
!         !    ngrid: number of grid points
!         !    idx: current point
!         !    lambda: cut-off in the jump
!         !    probnmm: density estimations
!         !    distmm: distances matrix
!
!         INTEGER, INTENT(IN) :: ngrid
!         INTEGER, INTENT(IN) :: idx
!         DOUBLE PRECISION, INTENT(IN) :: lambda
!         DOUBLE PRECISION, DIMENSION(ngrid), INTENT(IN) :: probnmm
!         DOUBLE PRECISION, DIMENSION(ngrid,ngrid), INTENT(IN) :: distmm
!
!         INTEGER j
!         DOUBLE PRECISION dmin
!
!         dmin=1.0d10
!         qs_next=idx
!         DO j=1,ngrid
!            IF(probnmm(j)>probnmm(idx))THEN
!               IF((distmm(idx,j).LT.dmin) .AND. (distmm(idx,j).LT.lambda))THEN
!                  dmin=distmm(idx,j)
!                  qs_next=j
!               ENDIF
!            ENDIF
!         ENDDO
!      END FUNCTION qs_next
      
      DOUBLE PRECISION FUNCTION fkernel(D,period,sig2,vc,vp)
            ! Calculate the (normalized) gaussian kernel
            !
            ! Args:
            !    sig2: sig**2
            !    vc: voronoi center's vector
            !    vp: point's vector

            INTEGER, INTENT(IN) :: D
            DOUBLE PRECISION, INTENT(IN) :: period(D)
            DOUBLE PRECISION, INTENT(IN) :: sig2
            DOUBLE PRECISION, INTENT(IN) :: vc(D)
            DOUBLE PRECISION, INTENT(IN) :: vp(D)


            fkernel=(1/( (twopi*sig2)**(dble(D)/2) ))* &
                    dexp(-pammr2(D,period,vc,vp)*0.5d0/sig2)
                    
      END FUNCTION fkernel

      SUBROUTINE sortclusters(nk,clusters,prif)
         ! Sort the gaussians from the closest to prif
         ! Bubble-sort ordering is implemented here
         !
         ! Args:
         !    nk: number of gaussian clusters
         !    clusters: array containing gaussians parameters
         !    prif: reference point

         INTEGER, INTENT(IN) :: nk
         TYPE(gauss_type), DIMENSION(nk), INTENT(INOUT) :: clusters
         DOUBLE PRECISION, DIMENSION(D), INTENT(IN) :: prif

         TYPE(gauss_type) tmpgauss
         DOUBLE PRECISION distances(nk),tmpdistance
         INTEGER j,i
         LOGICAL :: swapped = .TRUE.

         ! calculate the distances of the means from the reference
         DO i=1,nk
            distances(i)=dot_product(clusters(i)%mean-prif,clusters(i)%mean-prif)
         ENDDO
         ! now we can sort using the distances
         ! will use bubble sort
         DO j=nk-1,1,-1
            swapped = .FALSE.
            DO i = 1, j
               IF (distances(i) > distances(i+1)) THEN
                  tmpdistance=distances(i)
                  distances(i)=distances(i+1)
                  distances(i+1)=tmpdistance
                  ! swap the clusters
                  tmpgauss=clusters(i)
                  clusters(i)=clusters(i+1)
                  clusters(i+1)=tmpgauss
                  swapped = .TRUE.
               END IF
            END DO
            IF (.NOT. swapped) EXIT
         ENDDO
      END SUBROUTINE sortclusters

   END PROGRAM pamm
